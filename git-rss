#!/usr/bin/env perl
use strict;
use warnings;
use autodie;
use File::Basename qw( basename );
use POSIX qw( strftime );
use HTML::Escape 'escape_html';
use Git_RSS_Items;

# Get a command output and clear it if it only contains whitespace
sub cmd_output(@) {
    my $result = `@_`;
    chomp $result;
    return ($result !~ /^\s+$/) ? $result : '';
}

my $now = strftime("%a, %d %b %Y %H:%m:%S %z", gmtime());

my $last = cmd_output qw( git log --max-count=1 --format=%aD ), @ARGV;

my $name = basename(cmd_output qw( git rev-parse --show-toplevel ));

my @remotes = split /\n/, cmd_output qw( git remote -v );
my %remotes;
foreach (@remotes) {
    my ($name, $location, $kind) = /^([^\t]+)\t(.+?) \((\w+)\)/;
    $remotes{$name}{$kind} = $location;
}

my ($branch) = cmd_output(qw( git symbolic-ref HEAD )) =~ m{([^/]+)$};
if ($branch) {
    $name .= " [$branch]";
}

print << "EORSS";
<?xml version="1.0" encoding="utf-8"?>
<rss version="2.0">
 <channel>
  <title>Git commits for $name</title>
  <docs>http://blogs.law.harvard.edu/tech/rss</docs>
  <pubDate>$last</pubDate>
  <lastBuildDate>$now</lastBuildDate>
  <ttl>60</ttl>
EORSS

if (exists $remotes{'origin'}{'fetch'}) {
    print "  <link>" . escape_html($remotes{'origin'}{'fetch'}) . "</link>\n";
}

my @items = git_items(@ARGV);
print "  $_" foreach (@items);

print << 'EORSS'
 </channel>
</rss>
EORSS
